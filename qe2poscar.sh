#!/bin/bash

# 自动搜寻当前目录下的 .xml 文件（有且只有一个）
xml_file=$(ls *.xml | head -n 1)

prefix=$(basename "$xml_file" .xml)
# POSCAR 文件名
poscar="${prefix}.vasp"

awk 'BEGIN {Bohr_radius=5.29177210903e-1; output=0;
            a1=a2=a3=""; alat=0; nat=0; ia=0;
            ntyp=0; atom=0;
            print "Generated by Shell script";
            print "1.00000000000000";}
/<output>/ {output=1;}
/<\/output>/ {output=0;}
output && /<a1>/ {gsub(/<[^>]+>/, "");
                    a1=$0;}
output && /<a2>/ {gsub(/<[^>]+>/, "");
                    a2=$0;}
output && /<a3>/ {gsub(/<[^>]+>/, "");
                    a3=$0;}
output && /<atomic_structure/ { split($0, arr, "\"");
                    nat=arr[2];
                    alat=arr[6];}
output && /<atom / {
    atom=1;
    match($0, /name="([^"]+)"/, arr)
    atmtmp=arr[1]
    if (ntyp==0) {
       ntyp=1;
       atm[ntyp]=atmtmp;
       na[ntyp]=1;}
    else {
       if (atmtmp==atm[ntyp]) {
          na[ntyp]++;}
       else {
          ntyp++;
          atm[ntyp]=atmtmp;
          na[ntyp]=1;}
     }
    gsub(/<[^>]+>/, "")
    split($0, tau_arr, " ")
    ia++
    if (length(tau_arr)==3) { # 如果 atom 不跨行
        for (i=1;i<=3;i++) {
           tau[ia,i]=tau_arr[i];}
        atom=0;
    }
    next;
}
atom {
     for (i=1;i<=3;i++) {
        tau[ia,i]=$i}
     atom=0;
}
END{
    split(a1, a1_arr, " ")
    split(a2, a2_arr, " ")
    split(a3, a3_arr, " ")
    for (i=1;i<=3;i++){
       a[1,i]=a1_arr[i]}
    for (i=1;i<=3;i++){
       a[2,i]=a2_arr[i]}
    for (i=1;i<=3;i++){
       a[3,i]=a3_arr[i]}
    for (i=1;i<=3;i++){
       for (j=1;j<=3;j++){
           a_si[i,j]=a[i,j]* Bohr_radius}
    } 
    for (i=1;i<=nat;i++){
       for (j=1;j<=3;j++){
           tau_si[i,j]=tau[i,j]* Bohr_radius}
    } 
    
    for (i=1;i<=3;i++){
        printf "%.16f %.16f %.16f\n", a_si[i,1],a_si[i,2],a_si[i,3];} 
    for (i = 1; i <= ntyp; i++) {
        printf "%s ", atm[i]
    }
    print ""
    for (i = 1; i <= ntyp; i++) {
        printf "%d ", na[i]
    }
    print ""
    print "Cartesian"
    for (i=1;i<=nat;i++){
        printf "%.16f %.16f %.16f\n", tau_si[i,1],tau_si[i,2],tau_si[i,3];} 
}' $xml_file > $poscar
